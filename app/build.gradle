/*
 * Copyright 2016 Futurice GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'checkstyle'

apply from: 'app-config.gradle'

def STRING = "String"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.futurice.freesound"
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        signingConfig signingConfigs.debug
        multiDexEnabled true

        buildConfigField(STRING, "FREESOUND_API_URL", freesound_api_url)
        buildConfigField(STRING, "FREESOUND_API_CLIENT_ID", freesound_api_client_id)
        buildConfigField(STRING, "FREESOUND_API_CLIENT_SECRET", freesound_api_client_secret)
    }
    signingConfigs {
        debug {
            storeFile file("keystore/debug.keystore")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    './proguard-rules/android-support-rules.pro',
                    './proguard-rules/autovalue-rules.pro',
                    './proguard-rules/okhttp3-rules.pro',
                    './proguard-rules/picasso-rules.pro',
                    './proguard-rules/retrofit2-rules.pro',
                    './proguard-rules/rxjava2-rules.pro',
                    './proguard-rules/okio-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        lintConfig file('./quality/lint.xml')
        abortOnError false
    }

}

task checkstyle(type: Checkstyle) {
    group 'verification'
    description 'Checks if the code adheres to the coding standards'
    configFile file('quality/checkstyle.xml')
    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files() // empty because unnecessary for checkstyle
}

ext {
    dependencyVersions = [
            kotlin                         : kotlin_version,
            androidSupportLibrary          : '25.3.1',
            androidSupportConstraintLibrary: '1.0.2',
            javaPoet                       : '1.9.0',
            dagger2                        : '2.4',
            javaxAnnotation                : '10.0-b28',
            rxJava2                        : '2.1.0',
            rxAndroid                      : '2.0.1',
            ixJava                         : '1.0.0-RC5',
            okHttp                         : '3.6.0',
            picasso                        : '2.5.2',
            retrofit2                      : '2.2.0',
            retrofit2RxJava2Adapter        : '1.0.0',
            retrofit2GsonConverter         : '2.0.0',
            gson                           : '2.8.1',
            autoValue                      : '1.4.1',
            autoValueGson                  : '0.4.6',
            autoValueParcelable            : '0.2.5',
            autoFactory                    : '1.0-beta5',
            options                        : '1.2.4',
            exoplayer                      : 'r2.1.1',
            butterknife                    : '8.6.0',
            firebase                       : '9.4.0',
            stetho                         : '1.4.2',
            chuck                          : '1.0.4',
            timber                         : '4.5.1',
            junit                          : '4.12',
            mockito                        : '2.2.2',
            assertJ                        : '1.7.1',
            leakCanary                     : '1.5']
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$dependencyVersions.kotlin"

    // Android Support
    implementation "com.android.support:support-v4:$dependencyVersions.androidSupportLibrary"
    implementation "com.android.support:support-annotations:$dependencyVersions.androidSupportLibrary"
    implementation "com.android.support:appcompat-v7:$dependencyVersions.androidSupportLibrary"
    implementation "com.android.support:palette-v7:$dependencyVersions.androidSupportLibrary"
    implementation "com.android.support:recyclerview-v7:$dependencyVersions.androidSupportLibrary"
    implementation "com.android.support:cardview-v7:$dependencyVersions.androidSupportLibrary"
    implementation "com.android.support:design:$dependencyVersions.androidSupportLibrary"
    implementation "com.android.support.constraint:constraint-layout:$dependencyVersions.androidSupportConstraintLibrary"

    // Dagger 2
    annotationProcessor "com.squareup:javapoet:$dependencyVersions.javaPoet"
    annotationProcessor "com.google.dagger:dagger-compiler:$dependencyVersions.dagger2"
    implementation "com.google.dagger:dagger:$dependencyVersions.dagger2"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:$dependencyVersions.rxAndroid"
    implementation "io.reactivex.rxjava2:rxjava:$dependencyVersions.rxJava2"

    // IxJava
    implementation "com.github.akarnokd:ixjava:$dependencyVersions.ixJava"

    // Networking
    implementation "com.squareup.okhttp3:okhttp:$dependencyVersions.okHttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$dependencyVersions.okHttp"
    implementation "com.squareup.picasso:picasso:$dependencyVersions.picasso"

    // Networking Retrofit
    implementation "com.squareup.retrofit2:retrofit:$dependencyVersions.retrofit2"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$dependencyVersions.retrofit2RxJava2Adapter"
    implementation "com.squareup.retrofit2:converter-gson:$dependencyVersions.retrofit2GsonConverter"

    // GSON
    implementation "com.google.code.gson:gson:$dependencyVersions.gson"

    // Auto-Value
    compileOnly "com.google.auto.value:auto-value:$dependencyVersions.autoValue"
    annotationProcessor "com.google.auto.value:auto-value:$dependencyVersions.autoValue"

    // Auto-Value GSON
    compileOnly "com.ryanharter.auto.value:auto-value-gson:$dependencyVersions.autoValueGson"
    annotationProcessor "com.ryanharter.auto.value:auto-value-gson:$dependencyVersions.autoValueGson"

    // Auto-Value Parcelable
    compileOnly "com.ryanharter.auto.value:auto-value-parcel-adapter:$dependencyVersions.autoValueParcelable"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:$dependencyVersions.autoValueParcelable"

    // AutoFactory
    compileOnly "com.google.auto.factory:auto-factory:$dependencyVersions.autoFactory"
    annotationProcessor "com.google.auto.factory:auto-factory:$dependencyVersions.autoFactory"

    // Optionals for Java 6/7
    implementation "com.github.tomaszpolanski:options:$dependencyVersions.options"

    // Audio player
    implementation "com.google.android.exoplayer:exoplayer:$dependencyVersions.exoplayer"

    // Butter Knife
    implementation "com.jakewharton:butterknife:$dependencyVersions.butterknife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$dependencyVersions.butterknife"

    // Timber
    implementation "com.jakewharton.timber:timber:$dependencyVersions.timber"

    // Instrumentation
    implementation "com.facebook.stetho:stetho:$dependencyVersions.stetho"
    implementation "com.facebook.stetho:stetho-okhttp3:$dependencyVersions.stetho"
    debugImplementation "com.readystatesoftware.chuck:library:$dependencyVersions.chuck"

    // Firebase
    implementation "com.google.firebase:firebase-core:$dependencyVersions.firebase"
    implementation "com.google.firebase:firebase-crash:$dependencyVersions.firebase"

    // Unit Testing
    testImplementation "junit:junit:$dependencyVersions.junit"
    testImplementation "org.mockito:mockito-core:$dependencyVersions.mockito"
    testImplementation "org.assertj:assertj-core:$dependencyVersions.assertJ"
    testImplementation "com.squareup.okhttp3:mockwebserver:$dependencyVersions.okHttp"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$dependencyVersions.leakCanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$dependencyVersions.leakCanary"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$dependencyVersions.leakCanary"

}

apply plugin: 'com.google.gms.google-services'
