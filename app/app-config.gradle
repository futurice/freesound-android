/*
 * Copyright 2016 Futurice GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    // Freesound API
    freesound_api_key = getFreesoundApiKey()
    freesound_api_url = "\"https://www.freesound.org/\""
}

def String getFreesoundApiKey() {
    String freesoundApiKey = isCiBuild() ? getApiKeyFromEnv() : getApiKeyFromFile();
    if (freesoundApiKey == null || freesoundApiKey.isEmpty()) {
        throw new IllegalStateException(
                "Could not find Freesound API key value in environment or property file")
    }
    return "\"$freesoundApiKey\""
}

private boolean isCiBuild() {
    System.getenv("TRAVIS") == "true"
}

private String getApiKeyFromFile() {
    Properties apiProperties = loadFileProperties("$projectDir/freesound-api.properties")
    return apiProperties.getProperty("freesound.api.key")
}

private String getApiKeyFromEnv() {
    System.getenv("FREESOUND_API_KEY")
}

def Properties loadFileProperties(String fileLocation) {
    def Properties properties = new Properties()
    try {
        properties.load(new FileInputStream(fileLocation))
    } catch (FileNotFoundException fnf) {
        logger.log(LogLevel.ERROR,
                String.format("Missing Freesound API properties file: %s", fileLocation),
                fnf)
        throw fnf
    }
    return properties
}
